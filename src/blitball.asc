dim map(8, 3, 8)
global map()
global tw,th
global mx,my
global blkCounter
global blkEntPtr

blkCounter = 0
blkEntPtr = 0

mx = 140
my = 0

tw = 38
th = 37

rem define actors
actorCount = 2
dim actorType(actorCount)
dim actor3Dx(actorCount)
dim actor3Dy(actorCount)
dim actor3Dz(actorCount)
dim actor2Dx(actorCount)
dim actor2Dy(actorCount)

rem Player
actorType(0) = 0
actor3Dx(0) = 1
actor3Dy(0) = 0
actor3Dz(0) = 1

rem Test enemy
actorType(1) = 1
actor3Dx(1) = 4
actor3Dy(1) = 0
actor3Dz(1) = 4



procedure ldTiles
  load iff "assets/gfx/tiles.iff",0
  screen hide 0
   x = 1
   y = 1
   o = 3
   gridWidth = 7
   gridX = 0
   _tileCount = 14
  for i = 0 to _tileCount
    if (gridX > 6) 
      gridX = 0
      x = 1
      y = y + th + o
    end if
    get block i+1, x, y, tw, th, 1
    x = x + tw + o
    gridX = gridX + 1
    blkCounter = i
  next i
end proc

procedure ldEntities
    load iff "assets/gfx/ent.iff",0
    screen hide 0
    ew = 38
    eh = 37
    x = 1
    y = 1
    o = 3
    gridWidth = 7
    gridX = 0
    _entCount = 14
  for i = 0 to _entCount
    if (gridX > 6) 
      gridX = 0
      x = 1
      y = y + eh + o
    end if
    get bob i+1, x, y to x+ew, y+eh
    x = x + ew + o
    gridX = gridX + 1
  next i
end proc

procedure ldEnts
  blkEntPtr = blkCounter
  load iff "assets/gfx/ent.iff",0
  screen hide 0
  ew = 38
  eh = 37
  x = 1
  y = 1
  o = 3
  gridWidth = 7
  gridX = 0
  _entCount = 14
  for i = 0 to _entCount
    if (gridX > 6) 
      gridX = 0
      x = 1
      y = y + eh + o
    end if
    get block blkEntPtr+i, x, y, tw, th, 1
    rem get bob i+1, x, y to x+ew, y+eh
    x = x + ew + o
    gridX = gridX + 1
  next i
end proc

procedure clearMapData
    for z = 0 to 7
        for y = 0 to 2
            for x = 0 to 7
                map(x, y, z) = 0
            next x
        next y
    next z
end proc

procedure basicFloor
    for z = 0 to 7
        for x = 0 to 7
        map(x, 0, z) = 1
        next x
    next z
end proc

procedure southWall
    for y = 0 to 2
        for x = 0 to 7
            map(x, y, 7) = 2
        next x
    next y
    map(4, 0, 7) = 1
    map(4, 1, 7) = 0
end proc

procedure westWall
    for y = 0 to 1
        for z = 0 to 7
            map(7, y, z) = 2
        next z
    next y
end proc

procedure initMapData
    clearMapData
    basicFloor
    southWall
    westWall
end proc

procedure iso2x[x,y,z,ox]
    xx = x * 18 - z * 18 + ox
end proc[xx]

procedure iso2y[x,y,z,oy]
    yy = x * 9 - y * 18 + z * 9 + oy
end proc[yy]

procedure drwMap    
    for z = 0 to 7
        for y = 0 to 3
            for x = 0 to 7
                t = map(x, y, z)
                if (t > 0)
                    iso2x[x,y,z,mx]
                    xx = param
                    iso2y[x,y,z,my]
                    yy = param
                    put block t, xx, yy
                end if
            next x
        next y
    next z
end proc

procedure tile[x,y,z]
    if (x < 0 or y < 0 or z < 0 or x > 7 or y > 2 or z > 7) then t = 0 else t = map(x,y,z)
end proc[t]

procedure drwClpMap[px,py,pz]
    for z = 0 to 4
        for y = 0 to 2
            for x = 0 to 4
                tile[x + px, y + py, z + pz]
                t = param
                if (t > 1)
                    iso2x[x, y, z, 0]
                    xx = param
                    iso2y[x, y, z, 0]
                    yy = param
                    put block t, xx, yy
                end if
            next x
        next y
    next z
end proc

screen open 1,64,64,32,lowres
cls 0
screen hide 1

screen hide 0

initMapData

ldTiles
ldEntities
ldEnts

cls 0


drwMap

double buffer
priority on

screen show 0

procedure idle[p]
    for i = 0 to p
    next i
end proc

do

    for i = 0 to actorCount - 2

        iso2x[actor3Dx(i),actor3Dy(i),actor3Dz(i),mx]
        xx = param
        iso2y[actor3Dx(i),actor3Dy(i),actor3Dz(i),my]
        yy = param

        screen 1
        cls 0
        put block blkEntPtr,0,0

        drwClpMap[actor3Dx(i),actor3Dy(i),actor3Dz(i)]

        get bob 1,0,0 to tw,th
        screen 0
        bob i,xx,yy,actorType(i)+1

    next i

    if key state(6) = true then actor3Dx(0) = actor3Dx(0) - 1
    if key state(7) = true then actor3Dx(0) = actor3Dx(0) + 1
    if key state(8) = true then actor3Dz(0) = actor3Dz(0) + 1
    if key state(9) = true then actor3Dz(0) = actor3Dz(0) - 1
    if (actor3Dx(0) < 0) then actor3Dx(0) = 0
    if (actor3Dx(0) > 7) then actor3Dx(0) = 7
    if (actor3Dz(0) < 0) then actor3Dz(0) = 0
    if (actor3Dz(0) > 7) then actor3Dz(0) = 7

    idle[200000]
    wait vbl

loop